PREFIX sio: <http://semanticscience.org/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix graph4code: <http://purl.org/twc/graph4code/>

SELECT distinct ?g ?fitfnname ?fit ?fitsource ?read ?readsource ?x ?xsource ?y ?ysource WHERE {
  {
    select distinct ?fitfn ?fitfnname where {
      ?fitfn skos:notation ?fitfnname.
      filter(regex(?fitfnname, "^fit"))
    }
  }
  graph ?g {

  ?fit a ?fitfn.
  ?fit sio:SIO_000300 ?fitsource.

  ?fit (sio:SIO_000230/^sio:SIO_000229)* ?x.
  ?x (sio:SIO_000230/^sio:SIO_000229) ?y.
  ?y (sio:SIO_000230/^sio:SIO_000229)* ?read.

  ?x sio:SIO_000300 ?xsource.
  ?y sio:SIO_000300 ?ysource.

  ?read a <http://purl.org/twc/graph4code/python/pandas.read_csv>.
  ?read sio:SIO_000300 ?readsource.

  }
}
